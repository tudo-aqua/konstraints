/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Copyright 2023-2026 The Konstraints Authors
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import com.vanniktech.maven.publish.JavadocJar.Dokka
import com.vanniktech.maven.publish.KotlinJvm
import org.jetbrains.dokka.gradle.tasks.DokkaGenerateModuleTask
import tools.aqua.MetadataExtension
import tools.aqua.commonSetup

plugins { id("com.vanniktech.maven.publish") }

repositories { mavenCentral() }

val metadata = project.extensions.create<MetadataExtension>("metadata")

mavenPublishing {
  publishToMavenCentral()
  if (project.findProperty("mavenPublishing.signing.skip") != "true") {
    signAllPublications()
  }

  configure(
      KotlinJvm(
          Dokka(tasks.named("dokkaGenerateModuleJavadoc")),
      )
  )

  // add kdoc artifact
  val kdocJar =
      tasks.register<Jar>("dokkaKdocJar") {
        archiveClassifier = "kdoc"
        val dokkaTask = tasks.named<DokkaGenerateModuleTask>("dokkaGenerateModuleHtml")
        from(dokkaTask.flatMap { it.outputDirectory })
      }
  project.publishing.publications.named<MavenPublication>("maven").configure { artifact(kdocJar) }

  pom { commonSetup(metadata) }
}
